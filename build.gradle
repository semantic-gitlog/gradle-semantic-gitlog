plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'pmd'
    id "io.freefair.lombok" version "4.1.6"
    id 'team.yi.semantic-gitlog' version '0.5.0'
}

group = 'team.yi.gradle.plugin'
description = 'A simple `Semantic Versioning` management tool based on `Conventional Commits`.'
version = '0.5.3'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
generateLombokConfig.enabled = false

checkstyle.toolVersion = '8.30'
pmd.toolVersion = '6.22.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api gradleApi()

    api 'team.yi.tools:semantic-gitlog:0.5.11'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.1'
}

lombok {
    version = "1.18.12"
}

gradlePlugin {
    plugins {
        changelogPlugin {
            id = 'team.yi.semantic-gitlog'
            implementationClass = 'team.yi.gradle.plugin.ChangelogPlugin'
            displayName = 'gradle-semantic-gitlog'
        }
    }
}

pluginBundle {
    website = 'https://github.com/semantic-gitlog/gradle-semantic-gitlog'
    vcsUrl = 'https://github.com/semantic-gitlog/gradle-semantic-gitlog'
    description = 'gradle-semantic-gitlog'
    tags = ['versioning', 'semantic-versioning', 'git', 'auto-versioning', 'version']

    plugins {
        changelogPlugin {
            id = 'team.yi.semantic-gitlog'
            displayName = 'gradle-semantic-gitlog'
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Checkstyle) {
    ignoreFailures = false

    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Pmd) {
    ruleSetFiles = files("${rootDir}/config/pmd/rulesets.xml")
    ruleSets = []
    ignoreFailures = false

    reports {
        xml.enabled false
        html.enabled true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = "gradle-semantic-gitlog"

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/semantic-gitlog/gradle-semantic-gitlog'
                inceptionYear = '2020'

                scm {
                    url = 'https://github.com/semantic-gitlog/gradle-semantic-gitlog'
                    connection = 'scm:git:git@github.com:semantic-gitlog/gradle-semantic-gitlog.git'
                    developerConnection = 'scm:git:git@github.com:semantic-gitlog/gradle-semantic-gitlog.git'
                }

                licenses {
                    license {
                        name = "MIT"
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                organization {
                    name = "Yi.Team"
                    url = 'https://yi.team/'
                }

                developers {
                    developer {
                        name = "ymind"
                        email = 'ymind@yi.team'
                        url = 'https://yi.team/'
                        organization = 'Yi.Team'
                        organizationUrl = 'https://yi.team/'
                    }
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/semantic-gitlog/gradle-semantic-gitlog/issues'
                }

                ciManagement {
                    system = 'Travis-CI'
                    url = 'https://travis-ci.org/semantic-gitlog/gradle-semantic-gitlog'
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            // def releasesRepoUrl = "https://repo.114xp.cn/repository/maven-releases/"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

// ./gradlew setVersion -P newVersion=2.1.3
task setVersion {
    println version

    if (project.hasProperty('newVersion')) {
        println "Set Project to new Version $newVersion"

        String s = buildFile.getText().replaceFirst("version = '$version'", "version = '$newVersion'")

        buildFile.setText(s)
    }
}

changelog {
    toRef = "dev"
    isUnstable = true
    preRelease = 'SNAPSHOT'

    jsonFile = file("${rootDir}/CHANGELOG.json")
    fileSets = [
        {
            template = file("${rootDir}/config/gitlog/CHANGELOG.md.mustache")
            target = file("${rootDir}/CHANGELOG.md")
        },
        {
            template = file("${rootDir}/config/gitlog/CHANGELOG.zh-cn.md.mustache")
            target = file("${rootDir}/CHANGELOG.zh-cn.md")
        }
    ]
    commitLocales = [
        "en"   : file("${rootDir}/config/gitlog/commit-locales.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-locales.zh-cn.md")
    ]
    scopeProfiles = [
        "en"   : file("${rootDir}/config/gitlog/commit-scopes.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-scopes.zh-cn.md")
    ]
    issueUrlTemplate = "https://github.com/semantic-gitlog/gradle-semantic-gitlog/issues/:issueId"
    commitUrlTemplate = "https://github.com/semantic-gitlog/gradle-semantic-gitlog/commit/:commitId"
    mentionUrlTemplate = "https://github.com/:username"
}

derive {
    toRef = "dev"
    isUnstable = true
    preRelease = 'SNAPSHOT'
    derivedVersionMark = "NEXT_VERSION:=="

    commitLocales = [
        "en"   : file("${rootDir}/config/gitlog/commit-locales.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-locales.zh-cn.md")
    ]
    scopeProfiles = [
        "en"   : file("${rootDir}/config/gitlog/commit-scopes.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-scopes.zh-cn.md")
    ]
}
