plugins {
    id 'java'
    id 'team.yi.semantic-gitlog' version '0.5.13'
}

group = 'team.yi.gradle.plugin.example'
version = '0.1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

// ./gradlew setNewVersion -P newVersion=2.1.3
task setNewVersion {
    println version

    if (project.hasProperty('newVersion')) {
        println "Set Project to new Version $newVersion"

        String s = buildFile.getText().replaceFirst("version = '$version'", "version = '$newVersion'")

        buildFile.setText(s)
    }
}

changelog {
    toRef = "master"
    isUnstable = true
    strategy = "slow"

    jsonFile = file("${rootDir}/CHANGELOG.json")
    fileSets = [
        {
            template = file("${rootDir}/config/gitlog/CHANGELOG.md.mustache")
            target = file("${rootDir}/CHANGELOG.md")
        },
        {
            template = file("${rootDir}/config/gitlog/CHANGELOG.zh-cn.md.mustache")
            target = file("${rootDir}/CHANGELOG.zh-cn.md")
        }
    ]
    commitLocales = [
        "en"   : file("${rootDir}/config/gitlog/commit-locales.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-locales.zh-cn.md")
    ]
    scopeProfiles = [
        "en"   : file("${rootDir}/config/gitlog/commit-scopes.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-scopes.zh-cn.md")
    ]
    issueUrlTemplate = "https://github.com/semantic-gitlog/gradle-semantic-gitlog/issues/:issueId"
    commitUrlTemplate = "https://github.com/semantic-gitlog/gradle-semantic-gitlog/commit/:commitId"
    mentionUrlTemplate = "https://github.com/:username"
}

derive {
    toRef = "master"
    isUnstable = true
    derivedVersionMark = "NEXT_VERSION:=="

    commitLocales = [
        "en"   : file("${rootDir}/config/gitlog/commit-locales.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-locales.zh-cn.md")
    ]
    scopeProfiles = [
        "en"   : file("${rootDir}/config/gitlog/commit-scopes.md"),
        "zh-cn": file("${rootDir}/config/gitlog/commit-scopes.zh-cn.md")
    ]
}
